# Place all the behaviors and hooks related to the matching controller here.
# All this logic will automatically be available in application.js.
# You can use CoffeeScript in this file: http://coffeescript.org/

Wetstamp.DEFAULTS =
    class: "bottom right"
    rotate: 0
    position: <%= Magick::SouthEastGravity.to_i %>
    tiled: false
    size: 25
    transparency: 0.25

class Wetstamp.StampPreset

  constructor: (el) ->
    @el = el
    {@class, @rotate, @position, @tiled} = $.extend( {}, Wetstamp.DEFAULTS, @el.data() );
    
    @el.on "click", () =>
      if Wetstamp.stamp?
        Wetstamp.stamp.setClassAndStyleFromPreset(@)
    @

  applyToForm: =>
    Wetstamp.form.rotate = @rotate
    Wetstamp.form.position = @position
    Wetstamp.form.tiled = @tiled
    @

class Wetstamp.Stamp

  constructor: (json) ->
    @id = json.id
    @url = json.image.url
    @el = $("#watermark")
    @redraw()
    @class = ""
    @size = Wetstamp.DEFAULTS.size
    @transparency = Wetstamp.DEFAULTS.transparency
    @

  applyToForm: () ->
    Wetstamp.form.stampImageId = @id
    Wetstamp.form.size = @size
    Wetstamp.form.transparency = @transparency
    @

  urlObject: =>
    obj = document.createElement('a')
    obj.href = @url
    obj

  fileName: =>
    pathArr = @urlObject().pathname.split('/')
    pathArr[pathArr.length - 1]

  updateFile: (json) ->
    @id = json.id
    @url = json.image.url
    @applyToForm()
    @redraw()

  setClassAndStyleFromPreset: (preset) =>
    @klass = preset.class
    preset.applyToForm()
    @redraw()

  changeSize: (delta) =>
    @size += delta
    @size = Math.max(@size, 0) # Max sure it's not less than 0
    @applyToForm()
    @redraw()

  changeTransparency: (delta) =>
    @transparency += delta
    @transparency = Math.max(@transparency, 0) # Max sure it's >= 0
    @transparency = Math.min(@transparency, 1) # Max sure it's <= 1
    @applyToForm()
    @redraw()

  redraw: =>
    style = "opacity: #{@transparency}; width: #{@size}%;"
    @el.html("<img src='"+@url+"' alt='"+@fileName()+"' class='"+@klass+"' style='"+style+"'/>")

class Wetstamp.StampForm

  constructor: (el) ->

    {@rotate, @position, @tiled, @size, @transparency} = Wetstamp.DEFAULTS

    @el = el

    # Things to watch and listen to
    @stampImageForm = @el.find("form#new_stamp_image")

    @stampImageForm.find("input").on "change", () =>
      @stampImageForm.submit()

    @stampImageForm.on "ajax:remotipartComplete", (e, data) ->
      stamp_json = JSON.parse(data.responseText)
      if Wetstamp.stamp?
        Wetstamp.stamp.updateFile(stamp_json)
      else
        Wetstamp.stamp = new Wetstamp.Stamp(stamp_json)
        Wetstamp.stamp.setClassAndStyleFromPreset(Wetstamp.STAMP_PRESETS[0])

    @el.on "click", "button.delta", (event) ->
      btn = $(event.currentTarget)

      delta = parseFloat(btn.data('delta'))
      
      switch btn.data('for')
        when "size"
          Wetstamp.stamp.changeSize delta
        when "transparency"
          Wetstamp.stamp.changeTransparency delta
    .on "click", "button.submit", (event) =>
      @submit()

    @

  errors: =>
    errors = []
    @el.find(".error-block").remove()
    @el.find(".has-error").removeClass('has-error')

    unless @stampImageId?
      error = "You need to upload a watermark"
      errors.push error

      input = @el.find("input#stamp_image_image").parent()
      input.addClass("has-error")
      input.append("<p class='help-block error-block'>#{error}</p>")
      
    errors

  submit: =>
    errors = @errors()
    if errors.length
      console.warn errors
      return false
    else
      console.log @stampImageId, @size, @transparency, @position, @rotate, @tiled
      $.ajax
        type: "POST"
        url: '/stamps'
        dataType: 'json'
        data:
          stamp:
            stamp_image_id: @stampImageId
            size:           @size
            transparency:   @transparency
            position:       @position
            rotate:         @rotate
            tiled:          @tiled
      .success (data,status,response) ->
        window.location = response.getResponseHeader('Location')

      return true

$(document).ready ->

  Wetstamp.STAMP_PRESETS = []
  for presetLink in $("a[data-role='watermark-preset']")
    preset = new Wetstamp.StampPreset($(presetLink))
    Wetstamp.STAMP_PRESETS.push preset
  
  Wetstamp.form = new Wetstamp.StampForm($("#wetstamp-form"))

  url = "<%= image_url 'default_stamp.png' %>"
  Wetstamp.stamp = new Wetstamp.Stamp({id: -1, image: {url: url}})
  Wetstamp.stamp.setClassAndStyleFromPreset(Wetstamp.STAMP_PRESETS[0]);
  